
execute store result score <dimension> variable run data get storage pandamium:local functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].xyzd[3]
function pandamium:utils/get/dimension_name/from_score
data remove storage pandamium:local functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].xyzd[3]

execute if data storage pandamium:local functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].name in pandamium:staff_world run loot replace block 5 0 0 contents loot {pools:[{rolls:1,entries:[{type:"minecraft:item",name:"minecraft:stone"}]}],functions:[{function:"minecraft:set_name",entity:"this",name:[{"text":"• ","color":"gray","font":"minecraft:uniform"},{"storage":"pandamium:local","nbt":"functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].slot",plain:true,"color":"gold"},": ",{"storage":"pandamium:local","nbt":"functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].xyzd[]",plain:true,"separator":" ","color":"yellow"}," in ",{"storage":"pandamium:temp","nbt":"dimension_name",interpret:true}," '",{"text":"","color":"white","italic":true,"extra":[{"storage":"pandamium:local","nbt":"functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].name","interpret":true}]},"'"]}]}
execute unless data storage pandamium:local functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].name in pandamium:staff_world run loot replace block 5 0 0 contents loot {pools:[{rolls:1,entries:[{type:"minecraft:item",name:"minecraft:stone"}]}],functions:[{function:"minecraft:set_name",entity:"this",name:[{"text":"• ","color":"gray","font":"minecraft:uniform"},{"storage":"pandamium:local","nbt":"functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].slot",plain:true,"color":"gold"},": ",{"storage":"pandamium:local","nbt":"functions.'pandamium:triggers/player_info/print_inspection_menu/*'.homes[0].xyzd[]",plain:true,"separator":" ","color":"yellow"}," in ",{"storage":"pandamium:temp","nbt":"dimension_name",interpret:true}]}]}
data modify storage pandamium:local functions."pandamium:triggers/player_info/print_inspection_menu/*".homes_formatted append from block 5 0 0 item.components.minecraft:custom_name

data remove storage pandamium:local functions."pandamium:triggers/player_info/print_inspection_menu/*".homes[0]
execute if data storage pandamium:local functions."pandamium:triggers/player_info/print_inspection_menu/*".homes[0] run function pandamium:triggers/player_info/print_inspection_menu/homes_loop
