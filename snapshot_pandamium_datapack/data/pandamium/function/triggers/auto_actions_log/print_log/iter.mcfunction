data modify storage pandamium:auto_actions this_action set from storage pandamium:auto_actions print_list[0]

data modify storage pandamium:local functions."pandamium:triggers/auto_actions_log/*".args set from storage pandamium:auto_actions this_action.date
function pandamium:utils/datetime/get_datetime_text with storage pandamium:local functions."pandamium:triggers/auto_actions_log/*"

execute if data storage pandamium:auto_actions this_action{type:'auto_jail'} if score @s staff_perms matches 1 run tellraw @s [{text:" ",color:"gold"},{nbt:"print_list[0].username",storage:"pandamium:auto_actions",color:"yellow"},{text:" was "},{text:"auto-jailed",color:"yellow"},{text:" for "},{nbt:"print_list[0].reason",storage:"pandamium:auto_actions",color:"red"},{text:" "},{text:"[⌛]",color:"dark_gray",hover_event:{action:"show_text",value:{storage:"pandamium:temp",nbt:"datetime_text.date_time",interpret:true}}}]
execute if data storage pandamium:auto_actions this_action{type:'auto_jail'} if score @s staff_perms matches 2.. run tellraw @s [{text:" ",color:"gold"},{nbt:"print_list[0].clickEvent",storage:"pandamium:auto_actions",interpret:true,extra:[{text:"[→]",color:"blue",hover_event:{action:"show_text",value:[{text:"Click to teleport to ",color:"blue"},{nbt:"print_list[0].username",storage:"pandamium:auto_actions"},{text:"'s "},{text:"Pre-Jail Location",bold:true}]}}]},{text:" "},{nbt:"print_list[0].username",storage:"pandamium:auto_actions",color:"yellow"},{text:" was "},{text:"auto-jailed",color:"yellow"},{text:" for "},{nbt:"print_list[0].reason",storage:"pandamium:auto_actions",color:"red"},{text:" "},{text:"[⌛]",color:"dark_gray",hover_event:{action:"show_text",value:{storage:"pandamium:temp",nbt:"datetime_text.date_time",interpret:true}}}]
execute if data storage pandamium:auto_actions this_action{type:'take'} run tellraw @s [{text:" ",color:"gold"},{text:"[→]",color:"blue",hover_event:{action:"show_text",value:[{text:"Click to teleport to ",color:"blue"},{text:"Staff World",bold:true}]},click_event:{action:"run_command",command:"trigger staff_world"}},{text:" "},{nbt:"print_list[0].username",storage:"pandamium:auto_actions",color:"yellow"},{text:"'s items were "},{text:"taken",color:"yellow"},{text:" "},{text:"[⌛]",color:"dark_gray",hover_event:{action:"show_text",value:{storage:"pandamium:temp",nbt:"datetime_text.date_time",interpret:true}}}]
execute if data storage pandamium:auto_actions this_action{type:'name_change'} run tellraw @s [{text:" ",color:"gold"},{text:"Migrated data from "},{nbt:"print_list[0].old_username",storage:"pandamium:auto_actions",color:"yellow"},{text:" to "},{nbt:"print_list[0].new_username",storage:"pandamium:auto_actions",color:"yellow"}]

data remove storage pandamium:auto_actions print_list[0]
execute if data storage pandamium:auto_actions print_list[0] run function pandamium:triggers/auto_actions_log/print_log/iter
