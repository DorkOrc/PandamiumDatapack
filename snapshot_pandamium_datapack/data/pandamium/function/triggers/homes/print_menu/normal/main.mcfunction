tellraw @s [{text:"======== ",color:"aqua"},{text:"Homes List",bold:true},{text:" ========"}]

data modify storage pandamium:temp unset_homes set value []
function pandamium:triggers/homes/print_menu/normal/list
execute store result score <length> variable run data get storage pandamium:temp unset_homes

execute if score @s gameplay_perms matches 3.. unless score @s hide_unset_homes matches 1 run tellraw @s [{text:"\nShow Unset Homes: ",color:"aqua",hover_event:{action:"show_text",value:[{text:"Click to cycle options for\n",color:"aqua"},{text:"Show Unset Homes",bold:true},{text:"",color:"dark_gray",extra:[{text:"\n• On (Default)",color:"white"},{text:"\n• Off"}]}]},click_event:{action:"run_command",command:"trigger options set -102"}},{text:"On",color:"yellow",bold:true}]
execute if score @s gameplay_perms matches 3.. if score @s hide_unset_homes matches 1 run tellraw @s [{text:""},\
        {text:"\n[",color:"dark_green",hover_event:{action:"show_text",value:[{text:"Available Home Slots: ",color:"dark_green"},{color:"green",storage:"pandamium:temp",nbt:"unset_homes[]",interpret:true,separator:{text:", ",color:"dark_green"}}]},extra:[{score:{name:"<length>",objective:"variable"}},{text:" Available Home Slots]"}]},\
        {text:"\n"},\
        {text:"Show Unset Homes: ",color:"aqua",hover_event:{action:"show_text",value:[{text:"Click to cycle options for\n",color:"aqua"},{text:"Show Unset Homes",bold:true},{text:"\n• Off (Default)",color:"dark_gray",extra:[{text:"\n• Off",color:"white"}]}]},click_event:{action:"run_command",command:"trigger options set -102"},extra:[{text:"Off",color:"yellow",bold:true}]},\
    ]
execute unless score @s show_home_numbers matches 1 run tellraw @s [{text:"Show Home Numbers: ",color:"aqua",hover_event:{action:"show_text",value:[{text:"Click to cycle options for\n",color:"aqua"},{text:"Show Home Numbers",bold:true},{text:"",color:"dark_gray",extra:[{text:"\n• Off (Default)",color:"white"},{text:"\n• On"}]}]},click_event:{action:"run_command",command:"trigger options set -10"}},{text:"Off",color:"yellow",bold:true}]
execute if score @s show_home_numbers matches 1 run tellraw @s [{text:"Show Home Numbers: ",color:"aqua",hover_event:{action:"show_text",value:[{text:"Click to cycle options for\n",color:"aqua"},{text:"Show Home Numbers",bold:true},{text:"\n• Off (Default)",color:"dark_gray",extra:[{text:"\n• On",color:"white"}]}]},click_event:{action:"run_command",command:"trigger options set -10"}},{text:"On",color:"yellow",bold:true}]

tellraw @s [{text:"\nℹ",color:"blue",hover_event:{action:"show_text",value:[{text:"Ways to create or replace a home:",color:"aqua"},{text:"\n• Click the ",color:"green",extra:[{text:"[+]",color:"dark_green"},{text:" next to that home in the list.\n• Run "},{text:"/trigger ",color:"gray",extra:[{text:"sethome",color:"aqua"},{text:" set "},{text:"<home slot>",color:"yellow"}]},{text:"."}]},{text:"\n\nWays to teleport to a home:"},{text:"\n• Click the ",color:"green",extra:[{text:"[→]",color:"blue"},{text:" next to that home in the list.\n• Run "},{text:"/trigger",color:"gray",extra:[{text:" home",color:"aqua"},{text:" set "},{text:"<home slot>",color:"yellow"}]},{text:".\n• Run "},{text:"/trigger ",color:"gray",extra:[{text:"home.",color:"aqua"},{text:"<named home id>",color:"dark_aqua"}]},{text:"."}]},{text:"\n\nWays to name or rename a home:"},{text:"\n• Click the ",color:"green",extra:[{text:"[✎]",color:"yellow"},{text:" next to that home in the list.\n• Run "},{text:"/trigger ",color:"gray",extra:[{text:"namehome",color:"aqua"},{text:" set "},{text:"<home slot>",color:"yellow"}]},{text:"."}]},{text:"\n\nWays to delete a home:",color:"aqua"},{text:"\n• Click the ",color:"green",extra:[{text:"[❌]",color:"dark_red"},{text:" next to that home in the list.\n• Run "},{text:"/trigger ",color:"gray",extra:[{text:"delhome",color:"aqua"},{text:" set "},{text:"<home slot>",color:"yellow"}]},{text:"."}]}]}},{text:" ",color:"green",extra:[{text:"Hover here",color:"aqua"},{text:" to learn how to"},{text:" create",color:"aqua"},{text:","},{text:" name",color:"aqua"},{text:","},{text:" delete",color:"aqua"},{text:" and"},{text:" teleport to",color:"aqua"},{text:" homes."}]}]

tellraw @s {text:"============================",color:"aqua"}
