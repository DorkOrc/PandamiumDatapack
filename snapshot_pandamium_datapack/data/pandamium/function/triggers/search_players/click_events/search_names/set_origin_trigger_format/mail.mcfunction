execute store result score <target_can_receive_mail> variable run function pandamium:triggers/mail/check_if_user_can_receive_mail with storage pandamium.db.players:io selected.entry
execute if predicate pandamium:player/min_staff_perms/moderator run scoreboard players set <target_can_receive_mail> variable 1
execute if score <target_can_receive_mail> variable matches -1 run loot replace block 5 0 0 contents loot {pools:[{rolls:1,entries:[{type:"minecraft:item",name:"minecraft:stone"}]}],functions:[{function:"minecraft:set_name",entity:"this",name:[{text:""},{text:"[✉+]",color:"gray",hover_event:{action:"show_text",value:[{text:"",color:"red"},{storage:"pandamium:temp",nbt:"display_name",interpret:true},{text:" has not joined since mailing was enabled! Once they join the game at least once, they will be elligible for receiving mail."}]}},{text:" "},{storage:"pandamium:temp",nbt:"display_name",interpret:true}]}]}
execute if score <target_can_receive_mail> variable matches ..-2 run loot replace block 5 0 0 contents loot {pools:[{rolls:1,entries:[{type:"minecraft:item",name:"minecraft:stone"}]}],functions:[{function:"minecraft:set_name",entity:"this",name:[{text:""},{text:"[✉+]",color:"gray",hover_event:{action:"show_text",value:[{text:"You may not send mail to ",color:"red"},{storage:"pandamium:temp",nbt:"display_name",interpret:true},{text:" currently."}]}},{text:" "},{storage:"pandamium:temp",nbt:"display_name",interpret:true}]}]}
$execute if score <target_can_receive_mail> variable matches 1 run loot replace block 5 0 0 contents loot {pools:[{rolls:1,entries:[{type:"minecraft:item",name:"minecraft:stone"}]}],functions:[{function:"minecraft:set_name",entity:"this",name:[{text:""},{text:"[✉+]",color:"blue",hover_event:{action:"show_text",value:[{text:"Click to send ",color:"blue"},{storage:"pandamium:temp",nbt:"display_name",interpret:true},{text:" mail"}]},click_event:{action:"run_command",command:"trigger mail set $(id)"}},{text:" "},{storage:"pandamium:temp",nbt:"display_name",interpret:true}]}]}
