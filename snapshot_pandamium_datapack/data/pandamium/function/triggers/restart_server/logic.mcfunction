execute if score @s restart_server matches 1.. if score <seconds_until_restart> global matches 1.. run return run tellraw @s [{text:"[Restart]",color:"dark_red"},{text:" A server restart has already been scheduled! ",color:"red"},{text:"[❌]",color:"dark_red",hover_event:{action:"show_text",text:[{text:"Click to ",color:"dark_red"},{text:"cancel",bold:true},{text:" the server restart"}]},click_event:{action:"run_command",command:"trigger restart_server set -3"}}]
execute if score @s restart_server matches 1.. unless score <seconds_until_restart> global matches 1.. run return run tellraw @s [{text:"[Restart]",color:"dark_red"},{text:" Confirm server restart: ",color:"red"},{text:"[✔]",color:"dark_green",hover_event:{action:"show_text",text:[{text:"Click to restart the server ",color:"dark_green"},{text:"now",bold:true}]},click_event:{action:"run_command",command:"trigger restart_server set -1"}},{text:" "},{text:"[⌛]",color:"blue",hover_event:{action:"show_text",text:[{text:"Click to restart the server ",color:"blue"},{text:"in 30 Seconds",bold:true}]},click_event:{action:"run_command",command:"trigger restart_server set -2"}}]

execute if score @s restart_server matches -1 run return run function pandamium:triggers/restart_server/force_server_restart

execute if score @s restart_server matches -2 if score <seconds_until_restart> global matches 1.. run return run tellraw @s [{text:"[Restart]",color:"dark_red"},{text:" Could not schedule a server restart! A server restart has already been scheduled. ",color:"red"},{text:"[❌]",color:"dark_red",hover_event:{action:"show_text",text:[{text:"Click to ",color:"dark_red"},{text:"cancel",bold:true},{text:" the server restart"}]},click_event:{action:"run_command",command:"trigger restart_server set -3"}}]
execute if score @s restart_server matches -2 unless score <seconds_until_restart> global matches 1.. run return run function pandamium:triggers/restart_server/schedule_server_restart

execute if score @s restart_server matches -3 if score <seconds_until_restart> global matches 1.. run return run function pandamium:triggers/restart_server/cancel_server_restart
execute if score @s restart_server matches -3 unless score <seconds_until_restart> global matches 1.. run return run tellraw @s [{text:"[Restart]",color:"dark_red"},{text:" Could not cancel the server restart! There is no server restart scheduled.",color:"red"}]
