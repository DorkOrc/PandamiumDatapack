scoreboard players set @a temp_1 1
scoreboard players set @a[scores={hide_auto_messages=1,gameplay_perms=3..}] temp_1 0

execute if score <next_auto_message> global matches 0 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green",hover_event:{action:"show_text",value:[{text:"Click to join the ",color:"aqua"},{text:"Discord Server",bold:true}]},click_event:{action:"open_url",url:"http://discord.pandamium.eu"}},{text:"[Info] ",color:"blue"},{text:"Click here",color:"aqua"},{text:" to join our official "},{text:"Discord",color:"aqua",bold:true},{text:" server and always "},{text:"stay up to date",color:"aqua"},{text:" with announcements and changes!"}]
execute if score <next_auto_message> global matches 1 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green",hover_event:{action:"show_text",value:[{text:"Click to ",color:"aqua"},{text:"Vote for Pandamium",bold:true}]},click_event:{action:"suggest_command",command:"/trigger vote"}},{text:"[Info] ",color:"blue"},{text:"Run "},{text:"/trigger vote",color:"yellow"},{text:" to "},{text:"get rewards",color:"aqua"},{text:", "},{text:"rank up",color:"aqua"},{text:" and "},{text:"support the server",color:"aqua"},{text:"!"}]
execute if score <next_auto_message> global matches 2 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green",hover_event:{action:"show_text",value:[{text:"Click to join the ",color:"aqua"},{text:"Discord Server",bold:true}]},click_event:{action:"open_url",url:"http://discord.pandamium.eu"}},{text:"[Info] ",color:"blue"},{text:"Support us on "},{text:"Patreon",color:"aqua"},{text:" to get access to "},{text:"many exclusive perks",color:"aqua"},{text:" and "},{text:"keep the server running",color:"aqua"},{text:"! Check out our "},{text:"Discord",color:"aqua",bold:true},{text:" for more info."}]
execute if score <next_auto_message> global matches 3 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green",hover_event:{action:"show_text",value:[{text:"Click to see the ",color:"aqua"},{text:"Options",bold:true},{text:" menu"}]},click_event:{action:"suggest_command",command:"/trigger options"}},{text:"[Info] ",color:"blue"},{text:"Don't like "},{text:'"Keep-Inventory"',color:"aqua"},{text:" being enabled by default? Toggle it off in the "},{text:"options menu",color:"aqua"},{text:" by running "},{text:"/trigger options",color:"yellow"},{text:"!"}]
execute if score <next_auto_message> global matches 4 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green"},{text:"[Info] ",color:"blue"},{text:"Remember!",color:"aqua"},{text:" Do "},{text:"NOT",underlined:true},{text:" build "},{text:"long-term bases",color:"aqua"},{text:" in the "},{text:"RTP Range ",color:"aqua"},{text:"â„¹",color:"blue",hover_event:{action:"show_text",value:[{text:"The RTP Range is from \n"},{text:"-25000 ~ -25000",color:"gray"},{text:" to "},{text:"25000 ~ 25000",color:"gray"},{text:"\nin the overworld."}]}},{text:", or you will "},{text:"risk it being found and raided",color:"aqua"},{text:"!"}]
execute if score <next_auto_message> global matches 5 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green"},{text:"[Info] ",color:"blue"},{text:"Teleport to your friends",color:"aqua"},{text:" and "},{text:"trade",color:"aqua"},{text:" with others using "},{text:"/trigger tpa",color:"yellow"},{text:"!"}]
execute if score <next_auto_message> global matches 6 run tellraw @a[scores={temp_1=1}] [{text:"",color:"green"},{text:"[Info] ",color:"blue"},{text:"Create "},{text:"custom home shortcuts",color:"aqua"},{text:" by giving your home a name!"}]

scoreboard players add <next_auto_message> global 1
execute unless score <next_auto_message> global matches 0..6 run scoreboard players set <next_auto_message> global 0

execute unless score <disable_auto_messages> global matches 1 run schedule function pandamium:impl/auto_messages_loop 480s
