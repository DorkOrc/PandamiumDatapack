$execute store result score <dimension> variable run data get storage pandamium.db:players selected.entry.data.homes.$(home).xyzd[3]
function pandamium:utils/get/dimension_name/from_score

$data modify storage pandamium:temp home_name set value '{"text":"Home $(home)","bold":true}'
$execute if data storage pandamium.db:players selected.entry.data.homes.$(home).name run data modify storage pandamium:temp home_name set value '["",[{"text":"","color":"white","italic":true},{"storage":"pandamium.db:players","nbt":"selected.entry.data.homes.$(home).name","interpret":true}]]'

$execute unless data storage pandamium.db:players selected.entry.data.homes.$(home) run tellraw @a[tag=source,limit=1] [{"text":"","color":"green"},{"text":"[+]","color":"dark_green","bold":true,"clickEvent":{"action":"run_command","value":"/trigger sethome set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to set ","color":"dark_green"},{"text":"Home $(home)","bold":true}," at your\nlocation"]}}," ",{"text":"Home $(home): ","bold":true},{"text":"Not Set","color":"gray"}]
$execute unless data storage pandamium.db:players selected.entry.data.homes.$(home) run return 0
$execute if data storage pandamium.db:players selected.entry.data.homes.$(home) unless score @s hide_coordinates matches 1 run tellraw @a[tag=source,limit=1] [{"text":"","color":"green"},{"text":"[→]","color":"blue","bold":true,"clickEvent":{"action":"run_command","value":"/trigger home set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to teleport to ","color":"blue"},{"storage":"pandamium:temp","nbt":"home_name","interpret":true}]}}," ",{"text":"[❌]","color":"dark_red","bold":true,"clickEvent":{"action":"run_command","value":"/trigger delhome set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to delete ","color":"dark_red"},{"storage":"pandamium:temp","nbt":"home_name","interpret":true}]}}," ",{"text":"[✎]","color":"yellow","bold":true,"clickEvent":{"action":"run_command","value":"/trigger namehome set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to rename ","color":"yellow"},{"storage":"pandamium:temp","nbt":"home_name","interpret":true}]}}," ",{"storage":"pandamium:temp","nbt":"home_name","interpret":true}," ",{"text":"ℹ","color":"blue","hoverEvent":{"action":"show_text","contents":[{"storage":"pandamium.db:players","nbt":"selected.entry.data.homes.$(home).xyzd[0]","color":"aqua"}," ",{"storage":"pandamium.db:players","nbt":"selected.entry.data.homes.$(home).xyzd[1]"}," ",{"storage":"pandamium.db:players","nbt":"selected.entry.data.homes.$(home).xyzd[2]"}," in ",{"nbt":"dimension_name","storage":"pandamium:temp"},{"text":"\nHome $(home)","color":"dark_gray"}]}}]
$execute if data storage pandamium.db:players selected.entry.data.homes.$(home) unless score @s hide_coordinates matches 1 run return 0
$execute if data storage pandamium.db:players selected.entry.data.homes.$(home) if score @s hide_coordinates matches 1 run tellraw @a[tag=source,limit=1] [{"text":"","color":"green"},{"text":"[→]","color":"blue","bold":true,"clickEvent":{"action":"run_command","value":"/trigger home set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to teleport to ","color":"blue"},{"storage":"pandamium:temp","nbt":"home_name","interpret":true}]}}," ",{"text":"[❌]","color":"dark_red","bold":true,"clickEvent":{"action":"run_command","value":"/trigger delhome set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to delete ","color":"dark_red"},{"storage":"pandamium:temp","nbt":"home_name","interpret":true}]}}," ",{"text":"[✎]","color":"yellow","bold":true,"clickEvent":{"action":"run_command","value":"/trigger namehome set $(home)"},"hoverEvent":{"action":"show_text","value":[{"text":"Click to rename ","color":"yellow"},{"storage":"pandamium:temp","nbt":"home_name","interpret":true}]}}," ",{"storage":"pandamium:temp","nbt":"home_name","interpret":true}," ",{"text":"ℹ","color":"blue","hoverEvent":{"action":"show_text","contents":[{"nbt":"dimension_name","storage":"pandamium:temp","color":"aqua"},{"text":"\nHome $(home)","color":"dark_gray"}]}}]
$execute if data storage pandamium.db:players selected.entry.data.homes.$(home) if score @s hide_coordinates matches 1 run return 0
